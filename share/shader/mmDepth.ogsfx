// Copyright (C) 2021, 2023 David Cattermole.
//
// This file is part of mmSolver.
//
// mmSolver is free software: you can redistribute it and/or modify it
// under the terms of the GNU Lesser General Public License as
// published by the Free Software Foundation, either version 3 of the
// License, or (at your option) any later version.
//
// mmSolver is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU Lesser General Public License for more details.
//
// You should have received a copy of the GNU Lesser General Public License
// along with mmSolver.  If not, see <https://www.gnu.org/licenses/>.
// --------------------------------------------------------------------
//
// Draw the depth pass, with a small offset to the Z-Depth value.

// Global variables provided by Maya.
uniform mat4 gWVPXf : WorldViewProjection;
uniform mat4 gWorld : World;
uniform mat4 gViewInverse : ViewInverse;

// How much to offset the Z-Depth?
//
// If you assume the depth buffer has at least 24 bits for the depth
// value, then the minimum difference between 2 values is:
//
//     1 / ((2 ^ 24) / 16) = 1.0 / 1048576.0
//
// Therefore, the Z-Depth offset should be at least plus or minus
// "1.0 / 1048576.0".
uniform float gDepthOffset = 0.0f;

// Vertex Shader inputs.
attribute VS_INPUT {
    vec3 Pos : POSITION;
    vec3 Nrm : NORMAL;
    vec2 UV : TEXCOORD0;
};

// Vertex Shader data outputs, to be used by the Pixel Shader.
attribute SHADER_DATA {
    vec3 Normal : NORMAL;
    vec4 WorldPosition : TEXCOORD0;
};

// Vertex Shader
GLSLShader VS_mmDepth {
    void main() {
        VS_OUTPUT.WorldPosition = gWorld * vec4(Pos, 1);
        VS_OUTPUT.Normal = Nrm;

        vec4 position = gWVPXf * vec4(Pos, 1);
        // Offset the Z-Depth of the position a little bit, to avoid
        // Z-fighting.
        position.z -= position.w * gDepthOffset;
        gl_Position = position;
    }
}

// Pixel Shader Outputs
attribute PIXEL_DATA {
    vec4 colorOut : COLOR0;
}

GLSLShader PS_mmDepth_Main {
    void main() {
        vec3 camera_pos = gViewInverse[3].xyz;
        vec3 camera_dir = (camera_pos - PS_INPUT.WorldPosition.xyz);
        vec3 camera_dir_norm = normalize(camera_dir);

        float dot_norm_dir = dot(PS_INPUT.Normal, camera_dir_norm);
        vec3 absolute_normal =
            vec3(abs(PS_INPUT.Normal.x),
                 abs(PS_INPUT.Normal.y),
                 abs(PS_INPUT.Normal.z));
        colorOut = vec4(absolute_normal, dot_norm_dir);
    }
}

// The technique, used to merge textures together.
technique Main {
    pass p0 {
        VertexShader(in VS_INPUT, out SHADER_DATA VS_OUTPUT) = VS_mmDepth;
        PixelShader(in SHADER_DATA PS_INPUT, out PIXEL_DATA) = PS_mmDepth_Main;
    }
}
